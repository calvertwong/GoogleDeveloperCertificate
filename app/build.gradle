apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = null

if(keystorePropertiesFile.exists()) {
// Initialize a new Properties() object called keystoreProperties.
    keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 29


    defaultConfig {
        applicationId "com.calvert.googledevelopercertificate"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    if(keystoreProperties != null) {
        signingConfigs {
            config {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }

        buildTypes {
            release {
                minifyEnabled true
                signingConfig signingConfigs.config
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }
    }

// To inline the bytecode built with JVM target 1.8 into
// bytecode that is being built with JVM target 1.6. (e.g. navArgs)


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation Libs.org_jetbrains_kotlin__kotlin_stdlib_jdk7
    implementation Libs.androidx_appcompat__appcompat
    implementation Libs.androidx_core_core_ktx
    implementation Libs.com_google_android_material__material
    implementation Libs.androidx_constraintlayout_constraintlayout
    implementation Libs.androidx_vectordrawable__vectordrawable
    implementation Libs.androidx_navigation__navigation_fragment
    implementation Libs.androidx_navigation__navigation_ui
    implementation Libs.androidx_lifecycle__lifecycle_extensions
    implementation Libs.androidx_navigation__navigation_fragment_ktx
    implementation Libs.androidx_navigation__navigation_ui_ktx
    testImplementation Libs.junit__junit
    androidTestImplementation Libs.androidx_test_ext__junit
    androidTestImplementation Libs.androidx_test_espresso__espresso_core
    implementation Libs.androidx_multidex__multidex

    implementation project(':donate')
    implementation project(':toasts')
    implementation project(':viewbinding')
    implementation project(':responsiveuiconstraintlayout')
    implementation project(':recyclerview')
    implementation project(':common')
    implementation project(':customview')
    implementation project(':menu')
    implementation project(':dialogs')
    implementation project(':tablayout')
}
